// ---------------------------- objects/common.tx ------------------------
//
// Shared be objects/grammar.py and scenarios/grammar.py
//

ObjectDeclaration:
      SpeechObjectDeclaration
    | SymbolicObjectDeclaration
;


SpeechObjectDeclaration:
    name = ID
    ('est'|'is')
    ('une'|'un'|'an'|'a')  // order is important
    type = ID
    '.'
    (o_
        (textBlock = TextBlock)
    _o) ?
    X
;

SymbolicObjectDeclaration:
    name = ID
    ':'
    type = ID
    (o_
        (textBlock = TextBlock)
    _o) ?
    X
;

SlotDeclaration:
      SpeechSlotDeclaration
    | SymbolicSlotDeclaration
;

SpeechSlotDeclaration:
    DefiniteArticle ?
    attribute = ID
    ('of'|'de')
    object = ID
    ('is'|'est')
    value = BASETYPE
    (o_
        (textBlock = TextBlock)
    _o) ?
    '.'
    X
;


SymbolicSlotDeclaration:
    object = ID
    '.'
    attribute = ID
    '='
    value = BASETYPE
    (o_
        (textBlock = TextBlock)
    _o) ?
    X
;


LinkDeclaration:
      SpeechLinkDeclaration
    | SymbolicLinkDeclaration
;

SpeechLinkDeclaration:
    source = ID
    association = ID
    target = ID
    '.'
    (o_
        (textBlock = TextBlock)
    _o) ?
    X
;

SymbolicLinkDeclaration:
    '('
    source = ID
    ','
    association = ID
    ','
    target = ID
    ')'
    (o_
        (textBlock = TextBlock)
    _o) ?
    X
;

    // TODO: LinkObjectDeclaration
