GlossaryModel:
    status=ModelStatus? 'glossary' 'model' (name=ID) ?
    ('{'
        textBlock=TextBlock
    '}')? ';'
    entries *= Entry
;

ModelStatus:
      'draft'
    | 'consolidated'
;

Entry:
    term=ID
    ('{'
        ( textBlock=TextBlock )?
        (
            (package = Package) ?
            (synonymPart = SynonymPart) ?
            (inflectionPart = InflectionPart) ?
            (label = Label) ?
            (translationPart = TranslationPart) ?
        )#
        // declarations *= Declaration
    '}')? ';'
;

Package:
    'package' ':' name=ID ';'
;

SynonymPart:
    'synonyms' ':' synonyms *= ID
    ('{'
        synonyms *= ID ';'
    '}')? ';'
;

InflectionPart:
    'inflections' ':' inflections *= ID
    ('{'
        inflections *= ID ';'
    '}')? ';'
;

Label:
    'label' ':' text=STRING ';'
;

TranslationPart:
    'translations'
    ('{'
        translations *= Translation
    '}')? ';'
;

Translation:
    language=ID ':' label=STRING ';'
;






TextBlock:
     textLines *= TextLine
;

TextLine[noskipws]:
    /\s*\|/ textTokens *= TextToken /\|;/
;

TextToken:
      TextReference
    | PlainText
;

TextReference:
    '`' text=/[^`]*/ '`'
;


PlainText:
    text=/[^`\|]*/
;


Comment:
    /#.*$/
;