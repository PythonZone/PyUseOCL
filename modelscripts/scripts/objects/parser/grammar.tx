ObjectModel:
    megamodelPart = MegamodelPart
    declarations *= Declaration
;

Declaration:
      ObjectDeclaration
    | SlotDeclaration
//    | LinkDeclaration
//    | ObjectLinkDeclaration
;

ObjectDeclaration:
      SpeechObject
    | SymbolicObject
;


SpeechObject:
    name = ID
    ('est'|'is')
    ('une'|'un'|'an'|'a')  // order is important
    type = ID
    '.'
    (o_
        (textBlock = TextBlock)
    _o) ?
    X
;

SymbolicObject:
    name = ID
    ':'
    type = ID
    (o_
        (textBlock = TextBlock)
    _o) ?
    X
;

SlotDeclaration:
      SpeechSlot
    | SymbolicSlot
;

SpeechSlot:
    Article ?
    attribute = ID
    ('of'|'de')
    object = ID
    ('is'|'est')
    value = BASETYPE
    (o_
        (textBlock = TextBlock)
    _o) ?
    '.'
    X
;

Article:
      /[T|t]he/
    | /[L|l] *'/
    | /[L|l]a/
    | /[L|l]es/
    | /[L|l]e/
;

SymbolicSlot:
    object = ID
    '.'
    attribute = ID
    '='
    value = BASETYPE
    (o_
        (textBlock = TextBlock)
    _o) ?
    X
;
